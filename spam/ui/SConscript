import os
import cons

Import('env')
env = env.Clone()

env.IncludeTBB()
env.IncludeOpenCV()
env.IncludewxWidgets(cons.LINK_STATIC)
env.IncludeBoost()
env.IncludeGsl()
env.IncludeGlib()
env.IncludeCairo()
env.IncludeFreeType()
env.IncludeHDF5()
env.IncludeICU()
env.IncludePython2()

if env['BUILD_TYPE']==cons.LINK_DEBUG:
    env.Append(LIBS = ['libboost_python27-vc141-mt-gd-x64-1_67'])
elif env['BUILD_TYPE']==cons.LINK_RELEASE:
    env.Append(LIBS = ['libboost_python27-vc141-mt-x64-1_67'])

env.Append(CPPDEFINES = [('PY_MAJOR_VERSION', 2)])
env.Append(CPPDEFINES = [('PY_MINOR_VERSION', 7)])
env.Append(CPPDEFINES = ['BOOST_PYTHON_NO_LIB', 'BOOST_PYTHON_STATIC_MODULE', 'BOOST_PYTHON_STATIC_LIB'])

spamsrcs = Glob(r"*.cpp")+Glob(r"cv/*.cpp")
spamincs = Glob(r"*.h")+Glob(r"cv/*.h")
spamsrcs += Glob(r"commands/*.cpp")
spamincs += Glob(r"commands/*.h")
spamsrcs += Glob(r"projexplorer/*.cpp")
spamincs += Glob(r"projexplorer/*.h")
spamsrcs += Glob(r"toplevel/*.cpp")
spamincs += Glob(r"toplevel/*.h")
spamsrcs += Glob(r"dlg/*.cpp")
spamincs += Glob(r"dlg/*.h")
spamsrcs += Glob(r"misc/*.cpp")
spamincs += Glob(r"misc/*.h")

spam = env.Program(target="spam", source=spamsrcs)
spam_install = env.Install('#', spam)

spamsrcs = [str(s) for s in spamsrcs]
spamincs = [str(s) for s in spamincs]
spamlocalincs = []
spamresources = []
spammisc = ['SConscript']

buildtarget = [s for s in spam if str(s).endswith('exe')]
spam_vs_proj = env.MSVSProject(target = 'spam' + env['MSVSPROJECTSUFFIX'],
                               srcs = spamsrcs,
                               incs = spamincs,
                               localincs = spamlocalincs,
                               resources = spamresources,
                               misc = spammisc,
                               buildtarget = buildtarget,
                               variant = env['VS_VARIANT'])

Return('spam', 'spam_install', 'spam_vs_proj')